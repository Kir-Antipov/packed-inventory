plugins {
    id "fabric-loom" version "1.4-SNAPSHOT"
    id "org.ajoberstar.grgit" version "4.1.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

String minecraftDependencyVersion = project.minecraft_version.split(/[+ -]/)[0]

group = project.maven_group
archivesBaseName = project.archives_base_name
version = "${project.mod_version}${getVersionMetadata()}" as Object

loom {
    accessWidenerPath = file("src/main/resources/packed-inventory.accesswidener")
}

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Cloth Config"
        url = "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel.cloth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}")
    modCompileOnly modLocalRuntime("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}")

    compileOnly "org.jetbrains:annotations:19.0.0"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand([
            "version": version,
            "minecraft": ">=${minecraftDependencyVersion}",
            "cloth_config": ">=${project.cloth_version}",
            "modmenu": ">=${project.modmenu_version}",
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    // Ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly.
    // If Javadoc is generated, this must be specified in that task too.
    // See http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    it.options.encoding = "UTF-8"
    it.options.release.set(17)
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE.md"
}

@SuppressWarnings("GrMethodMayBeStatic") // It cannot
String getVersionMetadata() {
    String mcVersion = "+${project.minecraft_version}"

    // Release builds don't require additional metadata
    String ref = System.getenv("GITHUB_REF")
    if (ref != null && ref.startsWith("refs/tags/")) {
        return mcVersion
    }

    // GitHub CI builds only
    String build_id = System.getenv("GITHUB_RUN_NUMBER")
    if (build_id != null && build_id.length() != 0) {
        return "${mcVersion}+build.${build_id}"
    }

    if (grgit != null && grgit.head() != null) {
        def id = grgit.head().abbreviatedId

        // Flag the build if the build tree is not clean
        if (!grgit.status().clean) {
            id += "-dirty"
        }

        return "${mcVersion}+rev.${id}"
    }

    // No tracking information could be found about the build
    return "${mcVersion}+unknown"
}
